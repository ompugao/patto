// Pest can parse indentation-sensitive language.
// https://pest.rs/book/grammars/syntax.html#indentation-sensitive-languages
//
// However, we do special care of newline in indentation for code/quote blocks, which makes it difficult to consider parser syntax.
// Instead, we use pest only for parsing each line, and manually analyze indentations, constructing structures.

line = ${ expr_command | statement }
//statement = ${ raw_sentence ~ trailing_properties }
statement = ${ SOI ~ ((expr_anchor | expr_task) | (expr_embed | expr_img|expr_builtin_symbols|expr_code_inline|expr_math_inline|expr_url_link|expr_local_file_link|expr_mail_link| expr_wiki_link | expr_property | expr_hr | raw_sentence)*) ~ trailing_properties? ~ WHITE_SPACE_INLINE* ~ EOI}
raw_sentence = @{ (!("[" | "]" | "{@" | WHITE_SPACE_INLINE+ ~ expr_anchor | WHITE_SPACE_INLINE+ ~ expr_task) ~ ANY)+ }
expr_hr = @{ "-"{5,} }
// WARN: statement_nestable must be the subset of statement
statement_nestable = ${ (expr_embed | expr_img|expr_builtin_symbols|expr_code_inline|expr_math_inline|expr_url_link|expr_local_file_link|expr_mail_link|expr_wiki_link  | raw_sentence)* }

expr_builtin_symbols = ${"[" ~ builtin_symbols ~ WHITE_SPACE_INLINE+ ~ statement_nestable ~"]"}
builtin_symbols = { (symbol_bold | symbol_italic | symbol_underline | symbol_deleted)+ }
symbol_bold = @{"*"}
symbol_italic = @{"/"}
symbol_underline = @{"_"}
symbol_deleted = @{"-"}

expr_img = ${ "[@img" ~ WHITE_SPACE_INLINE+ ~(img_alt_path_opts|img_path_alt_opts|img_path_opts) ~ "]"}
img_alt_path_opts = ${ alt_img ~ WHITE_SPACE_INLINE+ ~ img_path} // ~ (WHITE_SPACE_INLINE+ ~ img_opt_keyword_arg ~ "=" ~ img_opt_keyword_value)*}
img_path_alt_opts = ${ img_path ~ WHITE_SPACE_INLINE+ ~ alt_img} // ~ (WHITE_SPACE_INLINE+ ~ img_opt_keyword_arg ~ "=" ~ img_opt_keyword_value)*}
img_path_opts = ${ img_path} // ~ (WHITE_SPACE_INLINE+ ~ img_opt_keyword_arg ~ "=" ~ img_opt_keyword_value)*}
img_path = ${(URL | local_file)}
alt_img = ${escaped_string}
// see https://github.com/pest-parser/book/blob/master/src/examples/rust/literals.md#strings
escaped_string = ${ "\"" ~ inner_string ~ "\"" }
inner_string = ${ (raw_string|escape)*}
raw_string = @{ (!("\\" | "\"") ~ ANY)+ }
predefined = { "n" | "r" | "t" | "\\" | "0" | "\"" | "'" }
hex = _{ '0'..'9' | 'a'..'f' | 'A'..'F' }
unicode_hex = { hex{1, 6} }
byte       = { "x" ~ hex{2} }
unicode    = { "u" ~ "{" ~ unicode_hex ~ "}" }
escape     = @{ "\\" ~ (predefined | byte | unicode) }
img_opt_keyword_arg = @{ ASCII_ALPHANUMERIC+ }
img_opt_keyword_value = @{ (ASCII_ALPHANUMERIC|CJK|"-"|"/"|":"|"_")+ }
//local_file = @{ (ASCII_ALPHANUMERIC|CJK)+ ~ ("/"+))* ~ (ASCII_ALPHANUMERIC|CJK|"."|"_"|"-"|":"|"("|")")+}
//local_file = @{"./" ~ (!"]" ~ !("\"") ~ ANY)*}
local_file = @{((ASCII_ALPHANUMERIC|CJK|"_"|"-"|"."|" ")+ ~ "/")+ ~ (ASCII_ALPHANUMERIC|CJK|"_"|"-"|" ")+ ~ ("." ~ LETTER+)+}
// /([\w_\-\s0-9\.]+\/)+([\w_\-\s0-9\.]+)\.([^\s\]]*)/u


expr_wiki_link = ${"[" ~ (wiki_link_anchored | wiki_link | self_link_anchored) ~ "]"}
wiki_link_anchored = ${ wiki_link ~ expr_anchor}
//wiki_link = @{ !"@" ~ (!("[" | "]" | "://" | "#" ) ~ ANY)+ }
wiki_link = @{ (!("[" | "]" | "://" | "#" ) ~ ANY)+ }
self_link_anchored = { expr_anchor }

expr_url_link = ${"[" ~ (expr_title_url | expr_url_title | expr_url_url | expr_url_only )  ~ "]"}
expr_title_url = ${url_title ~ WHITE_SPACE_INLINE+ ~ URL}
expr_url_title = ${URL ~ WHITE_SPACE_INLINE+ ~ url_title}
expr_url_only = ${url_only}
expr_url_url = ${url_only ~ WHITE_SPACE_INLINE+ ~ url_only}
url_title = @{ ((non_url_like_char_except_space)+ ~ (" "+))* ~ (non_url_like_char_except_space+)}
non_url_like_char_except_space = @{(!(URL | "[" | "]"| " ") ~ ANY)}
url_only = ${URL}
// non_url_like_char = _{ !("://"|"[" | "]") ~ ANY }
URL = @{ ASCII_ALPHA+ ~ "://" ~ (ASCII_ALPHANUMERIC | NUMBER|CJK | "/" | ":" | "#" | "%" | "$" | "&" | "?" | "@" | "!" | "(" | ")" | "~" | "." | "=" | "+" | "*"| "-" | "_")+}

expr_local_file_link = ${"[" ~ (expr_local_file_title | expr_title_local_file | expr_local_file_only) ~ "]"}
expr_title_local_file = ${local_file_title ~ WHITE_SPACE_INLINE+ ~ local_file}
expr_local_file_title = ${local_file ~ WHITE_SPACE_INLINE+ ~ local_file_title}
expr_local_file_only = ${local_file}
local_file_title = @{ ((local_file_char)+ ~ (" "+))* ~ (local_file_char+)}
local_file_char = @{ (!("./"|"["|"]"|" ") ~ ANY) }

expr_mail_link = ${"[" ~ (expr_title_mail | expr_mail_title | expr_mail_mail | expr_mail_only )  ~ "]"}
expr_mail_title = ${MAIL ~ WHITE_SPACE_INLINE+ ~ mail_title}
expr_title_mail = ${mail_title ~ WHITE_SPACE_INLINE+ ~ MAIL}
expr_mail_only = ${mail_only}
expr_mail_mail = ${mail_only ~ WHITE_SPACE_INLINE+ ~ mail_only}
mail_title = @{ ((non_mail_like_char_except_space)+ ~ (" "+))* ~ (non_mail_like_char_except_space+)}
non_mail_like_char_except_space = @{(!(MAIL | "[" | "]"| " ") ~ ANY)}
mail_only = ${MAIL}
MAIL = @{ "mailto:" ~ MAIL_CHAR+ ~ "@" ~ (MAIL_CHAR+ ~ ".")+ ~ MAIL_CHAR+}
MAIL_CHAR = _{ ASCII_ALPHANUMERIC | "-" | "_" | "+" }

expr_embed = ${ "[@embed" ~ WHITE_SPACE_INLINE+ ~ (embed_title_url | embed_url_title | embed_url_url | embed_url_only) ~ "]"}
embed_title_url = ${url_title ~ WHITE_SPACE_INLINE+ ~ URL}
embed_url_title = ${URL ~ WHITE_SPACE_INLINE+ ~ url_title}
embed_url_url = ${URL ~ WHITE_SPACE_INLINE+ ~ URL}
embed_url_only = ${URL}

expr_command_line = ${ expr_command ~ trailing_properties? }
expr_command = ${ "[@" ~ builtin_commands ~ (WHITE_SPACE_INLINE+ ~ parameter)* ~ WHITE_SPACE_INLINE* ~ "]"}
builtin_commands = { command_code | command_math | command_quote | command_table }
command_code = @{"code"}
command_math = @{"math"}
command_quote = @{"quote"}
command_table = @{"table"}
parameter = ${ (ASCII_ALPHANUMERIC+ ~ "=" ~ escaped_string) | escaped_string | (ASCII_ALPHANUMERIC|LETTER|CJK|"="|"-"|"/"|":"|"_")+ }

expr_code_inline = ${ "[`" ~ WHITE_SPACE_INLINE* ~ code_inline ~ "`]" }
code_inline = @{ code_inline_char* }
code_inline_char = _{ !"`]" ~ ANY }

expr_math_inline = ${ "[$" ~ WHITE_SPACE_INLINE* ~ math_inline ~ "$]" }
math_inline = @{ math_inline_char* }
math_inline_char = _{ !"$]" ~ ANY }

// Property syntax: {@name arg1 key=value ...}
// Supports both positional args and keyword args
expr_property = { "{@" ~ property_name ~ (WHITE_SPACE_INLINE+ ~ (property_keyword_pair | property_positional_arg))* ~ "}" }
property_name = @{ ASCII_ALPHANUMERIC+ }
property_keyword_pair = ${ property_keyword_arg ~ "=" ~ property_keyword_value }
property_positional_arg = @{ (ASCII_ALPHANUMERIC|CJK|"_"|"-")+ }
property_keyword_arg = @{ ASCII_ALPHANUMERIC+ }
property_keyword_value = @{ (ASCII_ALPHANUMERIC|CJK|"-"|"/"|":"|"_")+ }

trailing_properties = ${ (WHITE_SPACE_INLINE+ ~ (expr_property | expr_anchor | expr_task))+ }  // ignore white spaces
expr_anchor = ${ "#" ~ anchor }
anchor = @{ (!(WHITE_SPACE_INLINE | "}" | "]" | "\r" | "\n") ~ ANY)+ }
expr_task = ${ (symbol_task_done | symbol_task_doing | symbol_task_todo ) ~ task_due }
symbol_task_done = @{"-"}
symbol_task_doing = @{"*"}
symbol_task_todo = @{"!"}
task_due = @{ ASCII_DIGIT{4} ~ "-" ~ ASCII_DIGIT{2} ~ "-" ~ ASCII_DIGIT{2} ~ ("T" ~ ASCII_DIGIT{2} ~ ":" ~ ASCII_DIGIT{2})? }  // TODO

WHITE_SPACE_INLINE = _{ " " | "\t" }
CJK = _{ HAN | HIRAGANA | KATAKANA | HANGUL | BOPOMOFO | "ãƒ¼"}  // TODO some CJK characters are not recognized somehow
